apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    buildToolsVersion = versions.buildTools
    compileSdkVersion versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 10300 // Also update 'src/main/AndroidManifest.xml#versionCode' value
        versionName '1.3.0' // Also update 'src/main/AndroidManifest.xml#versionName' value
        setProperty("archivesBaseName", "godotopenxr.${versionName}")

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    flavorDimensions "functionality"
    productFlavors {
        full {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
            externalNativeBuild {
                cmake {
                    arguments "-DFLAVOR=FULL"
                }
            }
        }
        stub {
            externalNativeBuild {
                cmake {
                    arguments "-DFLAVOR=STUB"
                }
            }
        }
    }
}

dependencies {
    if (rootProject.findProject(":godot:lib") != null) {
        // Used for development and debugging
        compileOnly(project(":godot:lib"))
    } else {
        compileOnly(fileTree(dir: "libs", include: ["godot-lib*.aar"]))
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlinVersion}"
}

// When using the aar binary only, the *.gdns and *gdnlib files need to be in the same location
// relative to the `assets` directory, so we recreate the relative directories and copy them prior
// to building the aar binary.
final relativeAddonsConfigDir = "addons/godot-openxr/config/"
final sourceDir = "../demo/$relativeAddonsConfigDir"
final targetDir = "src/main/assets/$relativeAddonsConfigDir"

task copyAddonsConfig(type: Copy) {
    doFirst {
        delete(targetDir)
    }
    from sourceDir
    into targetDir
}

preBuild.dependsOn(copyAddonsConfig)
